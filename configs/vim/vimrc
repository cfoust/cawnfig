" Caleb Foust's vimrc
" It does everything you want it to. Maybe too much.

" Sets the directory for swap files
set directory^=$HOME/.vim/tmp//

" The home directory for vim
let g:CawnHome = $HOME . '/.vim/'

" Useful for sourcing the path in variables.
function Src(path)
  exec 'source ' . g:CawnHome . a:path
endfunction

" The directory for language-specific settings
let g:CawnLang = g:CawnHome . 'lang/'

" Docstring lets you add documentation strings for
" any and all <leader> bindings. Since I have so many,
" it'd be nice to be able to search through them with
" fzf or ctrlp.
call Src('docstring.vim')

"================================================
" GENERAL CONFIGURATION SETTINGS
"================================================
" Only my grandma uses four spaces.
" JK now I do too, thanks grandma
set tabstop=2 expandtab shiftwidth=2
set autoindent
set smartindent

" Jump around and show matches as we search with /.
" Useful when you want to look to see if something's in
" the file but don't want to commit to jumping to it.
set incsearch
set gdefault

" Allow for buffers to be hidden without saving. Vim
" still warns you if you try to quit without saving changes
" to disk.
set hidden

" Fix backspacing to allow backspacing past the point of
" insert.
set backspace=start

" We need our fancy shmancy ruler
set ruler

" Change the leader to space
" WARNING: makes you a master programmer
let mapleader = "\<Space>"

" Makes it so joins over comments work correctly.
if v:version > 703 || v:version == 703 && has('patch541')
  set formatoptions+=j
endif

" No more statusline
set laststatus=0

" This is like something a superhero named Code Hero would say.
syntax on

" Necessary for true color support afaik
set termguicolors
set t_8f=[38;2;%lu;%lu;%lum
set t_8b=[48;2;%lu;%lu;%lum

"================================================

" Source file that includes all plugins from Plug and their
" configurations.
call Src('plugins.vim')

"================================================
" SPLIT COMMANDS
"================================================
" Make the default split behavior to be more logical
set splitbelow
set splitright

" Use tmux-like split commands
"================================================
"
"================================================
" QUICK FILE OPEN COMMANDS
"================================================
Doc ve "Open .vimrc"
nnoremap <leader>ve :e ~/.vim/vimrc <CR>
Doc vp "Open plugins.vim"
nnoremap <leader>vp :e ~/.vim/plugins.vim <CR>
Doc vb "Open .bashrc"
nnoremap <leader>vb :e ~/.bashrc <CR>
Doc vr "Reload .vimrc"
nnoremap <leader>vr :source ~/.vim/vimrc <CR>
"================================================

"================================================
" AUTOCMD COMMANDS FOR SYNTAX AND PROFIT
"================================================
" Some of this is taken from a TA of mine's .vimrc that
" he distributed to my Introduction to Operating systems class back
" in the fall of 2016.
if has("autocmd")
   " When editing a file, always jump to the last cursor position
   autocmd BufReadPost * if line("'\"") | exe "normal '\"" | endif

   "In makefiles, don't expand tabs to spaces, since actual tabs are needed,
   "and set indentation at 8 chars to be sure that all indents are tabs:
   autocmd FileType make     set noexpandtab shiftwidth=8
   autocmd FileType automake set noexpandtab shiftwidth=8
   autocmd BufReadPost *.py  setlocal nosmartindent tabstop=4 shiftwidth=4
   autocmd BufNewFile,BufRead *.jlex set syntax=java
   autocmd BufNewFile,BufRead *.cup set syntax=java
   autocmd BufNewFile,BufRead *.hbs set syntax=html
   autocmd BufNewFile,BufRead *.vim set tw=0

   " C family languages
   autocmd BufReadPost *.m,*.h,*.c,*.mm,*.cpp,*.hpp setlocal tabstop=4 shiftwidth=4

   " Forces 8 space tabs to be consistent with gofmt
   autocmd BufReadPost *.go  setlocal tabstop=8 softtabstop=8 shiftwidth=8 noexpandtab

   " Enforce JS coding style
   autocmd BufReadPost *.js call Src('lang/js/settings.vim')
endif
"================================================

"================================================
" MISCELLANEOUS BINDINGS AND SUCH
"================================================
" Easy file write
nnoremap <silent> <leader>w :w<CR>
" Easy write quit
nnoremap <silent> <leader>q :wq<CR>
nnoremap <silent> <leader>Q :q!<CR>

" Remove all trailing whitespace
nnoremap <silent> <leader>t<space> :let _s=@/<Bar>:%s/\s\+$//e<Bar>:let @/=_s<Bar>:nohl<CR>

" Whenever you replace a word, it is copied, too. Useful for jumping to the
" next occurrence.
nnoremap <silent> ciw yiwciw
" Jump to the next occurrence of the text in register 0. Use in tandem with
" the command above to make replacements quickly.
nnoremap <silent> <leader>n /<C-r>0<CR>

" Delete line below with <leader>d and above with <leader>D
nnoremap <silent> <leader>d :+d <CR>
nnoremap <silent> <leader>D :-d <CR>

" Delete the current file
nnoremap <silent> <leader>~ :call delete(expand('%')) \| bdelete!<CR>

" Same, but add whitespace with <leader>s
" Pretty similar to unimpaired.vim's implementation
nnoremap <silent> <leader>s :<C-u>put =repeat(nr2char(10),v:count)<Bar>execute "'[-1"<CR>
nnoremap <silent> <leader>S :<C-u>put!=repeat(nr2char(10),v:count)<Bar>execute "']+1"<CR>

" Select the last pasted text with gp
nnoremap gp `[v`]

" Quickly sort lines
vnoremap gs :sort<CR>

" Duplicate and jump to beginning of paste"
vnoremap <leader>h y`>p

" Yank to and paste from system clipboard
nnoremap <silent> <leader>C "+p
vnoremap <leader>C "+y

" Remap j and k to scroll up and down
nnoremap j <C-D>
nnoremap k <C-U>

" Jump around curly blocks with s
nnoremap s ]}
nnoremap S [{
vmap s ]}
vmap S [{

" Move between splits with double leader
nnoremap <leader><leader>h <C-W><C-H>
nnoremap <leader><leader>j <C-W><C-J>
nnoremap <leader><leader>k <C-W><C-K>
nnoremap <leader><leader>l <C-W><C-L>

" Quick jumps in quick list
map <c-n> :cn <CR>zz

" Normally in vim if you paste over text in virtual mode
" it puts it into your paste buffer. Annoying if you're
" jumping around and replacing text with gn. This little
" script fixes that.
"
" Taken from:
" https://sheerun.net/2014/03/21/how-to-boost-your-vim-productivity/
function! RestoreRegister()
  let @" = s:restore_reg
  return ''
endfunction
function! s:Repl()
  let s:restore_reg = @"
  return "p@=RestoreRegister()\<cr>"
endfunction
vmap <silent> <expr> p <sid>Repl()
"================================================

"================================================
" START PAGE
"================================================
" This functionality replaces the vim startup screen with
" something of my own.
" It was sourced from here:
" http://klamp.works/2016/01/01/vim-splash.html
fun! Start()

  "Create a new unnamed buffer to display our splash screen inside of.
  enew

  " Set some options for this buffer to make sure that does not act like a
  " normal winodw.
  silent! setlocal
        \ bufhidden=wipe
        \ colorcolumn=
        \ nobuflisted
        \ nocursorcolumn
        \ nocursorline
        \ nolist
        \ nonumber
        \ norelativenumber
        \ nospell
        \ noswapfile 

  " Hide the cursor but make it so it returns if you leave.
  augroup start_screen
    execute "autocmd BufLeave <buffer> set t_ve=" . &t_ve
    execute "autocmd VimLeave <buffer> set t_ve=" . &t_ve
  augroup END
  setlocal t_ve=

  exec ":r !$PCM_VIM_STARTUP"

  " When we are done writing out message set the buffer to readonly.
  setlocal
    \ nomodifiable
    \ nomodified

  " Just like with the default start page, when we switch to insert mode
  " a new buffer should be opened which we can then later save.
  nnoremap <buffer><silent> e :enew<CR>
  nnoremap <buffer><silent> i :enew <bar> startinsert<CR>
  nnoremap <buffer><silent> o :enew <bar> startinsert<CR>
endfun

" http://learnvimscriptthehardway.stevelosh.com/chapters/12.html
" Autocommands are a way of setting handlers for certain events.
" `VimEnter` is the event we want to handle. http://vimdoc.sourceforge.net/htmldoc/autocmd.html#VimEnter
" The cleene star (`*`) is a pattern to indicate which filenames this Autocommand will apply too. In this case, star means all files.
" We will call the `Start` function to handle this event.

" http://vimdoc.sourceforge.net/htmldoc/eval.html#argc%28%29
" The number of files in the argument list of the current window.
" If there are 0 then that means this is a new session and we want to display
" our custom splash screen.
if argc() == 0
  autocmd VimEnter * call Start()
endif

" HAHAHAHA I bet there's silly stuff in here
source ~/.vimprivate
